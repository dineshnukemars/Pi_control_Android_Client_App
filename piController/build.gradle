apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api project(":coreLibs")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Grpc and Protobuf
    implementation("io.grpc:grpc-kotlin-stub:0.1.3"){
        [new Tuple('com.google.protobuf','protobuf-java'),
         new Tuple( 'javax.annotation','javax.annotation-api')].each {
            exclude group: "${it.get(0)}", module: "${it.get(1)}"
        }
    }
    implementation "io.grpc:grpc-okhttp:1.30.0"
    implementation "io.grpc:grpc-protobuf-lite:1.30.0"
    implementation "io.grpc:grpc-stub:1.30.0"
    implementation group: 'io.grpc', name: 'grpc-core', version: '1.30.0'
    implementation 'org.apache.tomcat:annotations-api:6.0.53'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.12.3" }
    plugins {
        // Specify protoc to generate using kotlin protobuf plugin
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.30.0"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:0.1.3"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                // Generate Java gRPC classes
                grpc { option 'lite' }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { option 'lite' }
            }
        }
    }
}